apply plugin: 'maven'
apply plugin: 'signing'

archivesBaseName = project.group + '-' + project.name

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar, dependsOn: jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
// for more information https://docs.gradle.org/current/javadoc/org/gradle/external/javadoc/StandardJavadocDocletOptions.html
tasks.withType(Javadoc) {
    options.encoding = "UTF-8"
    options.charSet = "UTF-8"
    options.docEncoding = "UTF-8"
    options.linkSource = true
    options.author = true
    options.version = true
    // = 需要https才能生效
    options.links = ["https://docs.oracle.com/javase/8/docs/api"]
    options.docTitle = archivesBaseName
    options.tags = ["date","implSpec"]

}


artifacts {
    archives javadocJar, sourcesJar, jar
}


signing {
    sign jar, javadocJar, sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: "${ossrhUsername}", password: "${ossrhPassword}")
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: "${ossrhUsername}", password: "${ossrhPassword}")
            }

            pom.project {
                name project.name
                packaging 'jar'
                // optionally artifactId can be defined here
                description project.description
                url 'https://github.com/amrjlg/common'

                scm {
                    connection 'https://github.com/amrjlg/common.git'
                    developerConnection 'https://github.com/amrjlg/common.git'
                    url 'https://github.com/amrjlg/common'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'amrjlg'
                        name 'amrjlg'
                        email '2899297045@qq.com'
                    }
                }
            }
        }
    }

}